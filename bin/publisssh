#!/usr/bin/env coffee

optimist = require 'optimist'
path = require 'path'
Publisher = require '../lib/publisher'

cwd = process.cwd()

options = optimist.usage('''
  Usage:
    publisssh some/source some-bucket/some/destination -fr
''').options({
  f: alias: 'force', description: 'Replace newer existing files', default: false
  r: alias: 'remove', description: 'Delete remote files that don\'t exist locally', default: false
  d: alias: 'dry-run', description: 'Don\'t change anything remotely.', default: false
  k: alias: 'key', description: 'AWS access key ID'
  s: alias: 'secret', description: 'AWS secret access key'
  c: alias: 'config', description: 'Configuration file'
  h: alias: 'help', description: 'Show options'
  v: alias: 'version', description: 'Show the version number'
}).argv

if options.help
  optimist.showHelp()
  process.exit 0

if options.version
  {name, version} = require '../package'
  console.log "#{name} v#{version}"
  process.exit 0

# Unpack command line arguments
{_: [local, remote]} = options

try
  config = require path.resolve cwd, options.config || 'publisssh'
  options[option] = value for own option, value of config

local = path.resolve cwd, local || options.local || '.'
remote = remote || options.remote || path.basename cwd
[bucketFromRemote, prefixesFromRemote...] = remote.split path.sep
bucket = options.bucket || bucketFromRemote
prefix = options.prefix || prefixesFromRemote.join path.sep

publisher = new Publisher {local, bucket, prefix, options}
publisher.publish()
